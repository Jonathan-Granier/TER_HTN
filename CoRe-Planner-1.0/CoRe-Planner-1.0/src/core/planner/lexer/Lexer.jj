/* 
 * Copyright MAGMA TEAM Laboratory Leibniz. 
 * 
 * This software is a computer program whose purpose is to propose an 
 * environment for multi-agent planning.
 * 
 * This software is governed by the CeCILL  license under French law and
 * abiding by the rules of distribution of free software.  You can  use, 
 * modify and/ or redistribute the software under the terms of the CeCILL
 * license as circulated by CEA, CNRS and INRIA at the following URL
 * "http://www.cecill.info". 
 * 
 * As a counterpart to the access to the source code and  rights to copy,
 * modify and redistribute granted by the license, users are provided only
 * with a limited warranty  and the software's author,  the holder of the
 * economic rights,  and the successive licensors  have only  limited
 * liability. 
 * 
 * In this respect, the user's attention is drawn to the risks associated
 * with loading,  using,  modifying and/or developing or reproducing the
 * software by the user in light of its specific status of free software,
 * that may mean  that it is complicated to manipulate,  and  that  also
 * therefore means  that it is reserved for developers  and  experienced
 * professionals having in-depth computer knowledge. Users are therefore
 * encouraged to load and test the software's suitability as regards their
 * requirements in conditions enabling the security of their systems and/or 
 * data to be ensured and,  more generally, to use and operate it in the 
 * same conditions as regards security. 
 * 
 * The fact that you are presently reading this means that you have had
 * knowledge of the CeCILL license and that you accept its terms.
 */ 

options {
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = true;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
}

PARSER_BEGIN(Lexer)

package core.planner.lexer;

import core.planner.*;
import java.io.*;

public class Lexer {
	/**
	* Create a <tt>Lexer</tt> from a specific <tt>InputStream<tt>.
	* @param stream the file <tt>InputStream</tt>.
	*/
	public Lexer(InputStream stream, boolean debug) {
		this(stream);
	}
}

PARSER_END(Lexer)

SKIP : 
{ 
	" "
| 	"\t"
|	"\n" 
| 	"\r"
}
MORE : 
{
	"//" : IN_SINGLE_LINE_COMMENT 
|	";" : IN_SINGLE_LINE_COMMENT 
|	<"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|	"/*" : IN_MULTI_LINE_COMMENT 
}
<IN_SINGLE_LINE_COMMENT>
	SPECIAL_TOKEN : { 
		<SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT 
	}
<IN_FORMAL_COMMENT>
	SPECIAL_TOKEN :	{
		<FORMAL_COMMENT: "*/" > : DEFAULT
	}
<IN_MULTI_LINE_COMMENT>
	SPECIAL_TOKEN : {
		<MULTI_LINE_COMMENT: "*/" > : DEFAULT
	}
<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT> 
	MORE : {
		< ~[] > 
}
TOKEN : {
	< DEFDOMAIN : "defdomain" >
|	< DEFPROBLEM : "defproblem" >
| 	< NOT: "not">
| 	< CALL: "call">
|	< INVARIANT_CALL : "~call" >
| 	< LIST: "list">
| 	< FIRST: ":first">
| 	< OPERATOR: ":operator">
| 	< METHOD: ":method">
}
TOKEN :
{
  	< LPAREN: "(">
| 	< RPAREN: ")">
| 	< DOT:	".">
| 	< AXIOM: ":-">
}
TOKEN :
{
	< SUP : ">" >
|	< INF : "<" >
|	< SUP_EQUAL : ">=" >
|	< INF_EQUAL : "<=" >
|	< MULT : "*" >
|	< DIV : "/" >
|	< PLUS : "+" >
|	< MINUS : "-" >
|	< EQUAL : "=" >
|	< NOT_EQUAL : "!=" >
|	< MEMBER : "member" >
|	< MIN : "min" >
|	< MAX : "max" >
|	< CAT : "cat" >
}
TOKEN :
{
	< SYMBOL : (<LETTER> (<LETTER> | <DIGIT>)*) >
|	< VARIABLE_SYMBOL : "?"<SYMBOL> >
|	< INVARIANT_SYMBOL : "~"<SYMBOL> >
|	< PRIMITIVE_TASK_SYMBOL : "!"<SYMBOL> > 
|	< NUMBER :  (<DIGIT>)+ >
|	< #DIGIT : ["0"-"9"] >
|	< #LETTER : ["a"-"z","A"-"Z","_","-","#"] > 
}

/**
* Read an AgentDescription from the input stream.
* @return the AgentDescription read.
*/
AgentDescription agentDescription():
{
	AgentDescription agentDescription = new AgentDescription();
	Problem problem = new Problem();
	Domain domain = new Domain();
}
{
	domain=domain() {agentDescription.setDomain(domain);}
	(problem=problem() {
		agentDescription.addProblem(problem);
	})*
	{return agentDescription;}
}

/**
* Read a list of problems from the input stream.
* @return the ProblemSet read.
*/
ProblemSet problemSet():
{
	ProblemSet problemSet = new ProblemSet();
	Problem problem = new Problem();
}
{
	(problem=problem() {
		if (!problemSet.add(problem)) {
				throw new ParseException("Problem " + problem.getName() + " line: " +
					getNextToken().beginLine + " column: " +
					getNextToken().beginColumn+" already defined.\n");
		}
	})*
	{return problemSet;}
}

/**
* Read a problem from the input stream.
* @return the problem read.
*/
Problem problem():
{
	Problem problem = new Problem();
	State state;
	TaskList tasks;
	Token token;
}
{
	<LPAREN> <DEFPROBLEM> 
		token=<SYMBOL> {problem.setName(token.image);}
		token=<SYMBOL> {problem.setDomainName(token.image);}
		state=state() {problem.setState(state);}
		[tasks=taskList() {problem.setTasks(tasks);}]
	<RPAREN> { 
		return problem;
	}
}
/**
* Read a State from the input stream.
* @return the State read.
*/
State state():
{
	Token token;
	Term t;
	Literal literal;
	State state;
}
{
	LOOKAHEAD(<SYMBOL>)
		token=<SYMBOL> {
			if (token.image.equals("nil")) {
				return new State();
			}
			else {
				throw new ParseException("Expected \"nil\" line: "+
					token.beginLine + " column: "+
					token.beginColumn + "\n");
			}
		}
|	LOOKAHEAD(<LPAREN>)
		{state = new State();}
		<LPAREN> 
		(literal=literal() {
			if (!state.add(literal)) {
				throw new ParseException("Literal " + literal + " line: "+
					getNextToken().beginLine + " column: " +
					getNextToken().beginColumn+" already defined.\n");
			}
		})*
		<RPAREN> {return state;}		
}
/**
* Read a Domain from the input stream.
* @return the Domain read.
*/
Domain domain():
{
	Domain domain = new Domain();
	Axiom axiom;
	AxiomSet axioms = new AxiomSet();
	Operator operator;
	OperatorSet operators = new OperatorSet();
	Method method;
	MethodSet methods = new MethodSet();
	Token token;
}
{
	<LPAREN> <DEFDOMAIN> token=<SYMBOL> <LPAREN> {
		domain.setName(token.image);
	}
	(
		LOOKAHEAD(2) ( axiom=axiom() {
			axiom.setName("Axiom_"+(axioms.size()+1)+"_");
			if (!axioms.add(axiom)) {
				throw new ParseException("Axiom " + axiom.getHead() + " line: " +
					getNextToken().beginLine + " column: " +
					getNextToken().beginColumn+" already defined.\n");
			}

		})
	|	LOOKAHEAD(2) ( operator=operator() {
			if (!operators.add(operator)) {
				throw new ParseException("Operator " + operator.getHead() + " line: " +
					getNextToken().beginLine + " column: " +
					getNextToken().beginColumn + " already defined.\n");
			}
		})
	|	LOOKAHEAD(2) (method=method() {
			method.setName("Method_"+(methods.size()+1)+"_");
			if (!methods.add(method)) {
				throw new ParseException("Method " + method.getHead() + " line: " +
					getNextToken().beginLine + " column: " +
					getNextToken().beginColumn + " already defined.\n");
			}
		})
	)*
	<RPAREN> <RPAREN> {
		domain.setAxioms(axioms);
		domain.setOperators(operators);
		domain.setMethods(methods);
		return domain;
	}
}
/**
* Read an Operator from the input stream.
* @return the Operator read.
*/
Operator operator():
{
	TaskAtom head;
	Conjunction precondition;
	Conjunction delEffect;
	Conjunction addEffect;
	double cost = 1;
	Token token;
}
{
	<LPAREN> <OPERATOR>
		head=taskAtom() 
		precondition=conjunction()
		delEffect=conjunction()
		addEffect=conjunction()
		[token=<NUMBER> {cost = Double.parseDouble(token.image);}]
	<RPAREN>
	{
		Operator operator = new Operator();
		operator.setHead(head);
		operator.setPreconditions(precondition);
		operator.setDelEffects(delEffect);
		operator.setAddEffects(addEffect);
		operator.setCost(cost);
		return operator;
	}
}
/**
* Read a Method from the input stream.
* @return the Method read.
*/
Method method():
{
	TaskAtom head;
	DecompositionList tail;
}
{
	<LPAREN> <METHOD>
		head=taskAtom()
		tail=decompositionList()
	<RPAREN>
	{
		Method method = new Method();
		method.setHead(head);
		method.setTail(tail);
		return method;
	}
}
/**
* Read a DecompositionList from the input stream.
* @return the DecompositionList read.
*/
DecompositionList decompositionList():
{
	int index = 1;
	Decomposition decomposition;
	DecompositionList decompositionList = new DecompositionList();
}
{
	(decomposition=decompositionName(index) {
		decompositionList.add(decomposition);
		index++;
	})*
	{return decompositionList;}
}
/**
* Read a Decomposition at a specific index from the input stream.
* @param index the index of the Decomposition.
* @return the Decomposition read.
*/
Decomposition decompositionName(int index):
{
	Token token;
	String name;
	Decomposition decomposition;
}
{
	LOOKAHEAD(<SYMBOL> decomposition())
		token=<SYMBOL> {name = token.image;}
		decomposition=decomposition() {
			decomposition.setName("Part_"+index);
			return decomposition;
		}
|	LOOKAHEAD(decomposition())
		decomposition=decomposition() {
			decomposition.setName("Part_"+index);
			return  decomposition;
		}
}
/**
* Read a Decomposition from the input stream.
* @return the Decomposition read.
*/
Decomposition decomposition():
{
	Conjunction precondition;
	TaskList tasks;
	boolean isFirst = false; 
}
{
	LOOKAHEAD(<LPAREN> <FIRST>) { isFirst = true;}
		precondition=conjunction()
		tasks=taskList()
	{
		Decomposition decomposition = new Decomposition();
		decomposition.setFirst(isFirst);
		decomposition.setPreconditions(precondition);
		decomposition.setTasks(tasks);
		return decomposition;
	}
|	LOOKAHEAD(<LPAREN>) { isFirst = false;}
		precondition=conjunction()
		tasks=taskList()
	{
		Decomposition decomposition = new Decomposition();
		decomposition.setFirst(isFirst);
		decomposition.setPreconditions(precondition);
		decomposition.setTasks(tasks);
		return decomposition;
	}
}
/**
* Read a TaskList from the input stream.
* @return the TaskList read.
*/
TaskList taskList(): 
{
	Token token;
	TaskAtom taskAtom;
	TaskList tasks = new TaskList();
}
{
	LOOKAHEAD(<SYMBOL>)
		token=<SYMBOL> {
			if (token.image.equals("nil")) {
				return tasks;
			}
			else {
				throw new ParseException("Expected \"nil\" line: "+
					token.beginLine+" column: "+
					token.beginColumn+"\n");
			}
		}
|	LOOKAHEAD(<LPAREN>)
		token=<LPAREN> 
			(taskAtom=taskAtom() {
				tasks.add(taskAtom);
			})*
		<RPAREN> {return tasks;}
}
/**
* Read a TaskAtom from the input stream.
* @return the TaskAtom read.
*/
TaskAtom taskAtom():
{
	Token token;
	Term term;
	TaskAtom taskAtom;
}
{
	LOOKAHEAD(<LPAREN> <PRIMITIVE_TASK_SYMBOL>)
		<LPAREN> 
			token=<PRIMITIVE_TASK_SYMBOL> {
				taskAtom = new TaskAtom(token.image);	
			 	taskAtom.setPrimitive(true);
			}
			(term=term() {taskAtom.add(term);})* 
		<RPAREN> {return taskAtom;}
|	LOOKAHEAD(<LPAREN> <SYMBOL>)
		<LPAREN> 
			token=<SYMBOL> {
				taskAtom = new TaskAtom(token.image);
				taskAtom.setCompound(true);
			}
			(term=term() {
				taskAtom.add(term);
			})* 
		<RPAREN> {return taskAtom;}		
}
/**
* Read a Axiom from the input stream.
* @return the Axiom read.
*/
Axiom axiom():
{
	Literal head;
	ConjunctionList tail;
}
{
	<LPAREN> <AXIOM>
		head=literal()
		tail=conjunctionList()
	<RPAREN> {
		Axiom axiom = new Axiom();
		axiom.setHead(head);
		axiom.setTail(tail);
		return axiom;
	}
}
/**
* Read a ConjunctionList from the input stream.
* @return the ConjunctionList read.
*/
ConjunctionList conjunctionList():
{
	int index = 1;
	Conjunction conjunction;
	ConjunctionList conjunctionList = new ConjunctionList();
}
{
	(conjunction=conjunctionName(index) {
		conjunctionList.add(conjunction);
		index++;
	})*
	{return conjunctionList;}
}
/**
* Read a Conjunction at a specific index from the input stream.
* @param index the index of the Conjunction.
* @return the Conjunction read.
*/
Conjunction conjunctionName(int index):
{
	Token token;
	String name;
	Conjunction conjunction;
}
{
	LOOKAHEAD(<SYMBOL> conjunction())
		token=<SYMBOL> {name = token.image;}
		conjunction=conjunction() {
			conjunction.setName("Part_"+name);
			return conjunction;
		}
|	LOOKAHEAD(conjunction())
		conjunction=conjunction() {
			conjunction.setName("Part_"+index);
			return  conjunction;
		}
}
/**
* Read a Conjunction from the input stream.
* @return the Conjunction read.
*/
Conjunction conjunction():
{
	Token token;
	Term t;
	Literal literal;
	Conjunction conjunction;
}
{
	LOOKAHEAD(<SYMBOL>)
		token=<SYMBOL> {
			if (token.image.equals("nil")) {
				return new Conjunction();
			}
			else {
				throw new ParseException("Expected \"nil\" line: "+
					token.beginLine+" column: "+
					token.beginColumn+"\n");
			}
		}
|	LOOKAHEAD(<VARIABLE_SYMBOL>) 
		t=term() {
			conjunction = new Conjunction();
			literal = new Literal();
			literal.add(t);
			conjunction.add(literal);
			conjunction.setVarList(true);
			return conjunction;
		}		
|	LOOKAHEAD(<LPAREN>)
		{conjunction = new Conjunction();}
		<LPAREN> 
			[token=<FIRST> {conjunction.setLabel(token.image);}]
		(literal=literal() {
			conjunction.add(literal);
		})*
		<RPAREN> {return conjunction;}		
}
/**
* Read a Literal from the input stream.
* @return the Literal read.
*/
Literal literal(): 
{
	Token token;
	Literal literal;
}
{
	LOOKAHEAD(<LPAREN> <NOT> logicalAtom()) 
		<LPAREN> <NOT>
			literal=logicalAtom()
		<RPAREN> {
			literal.setNegative(true);
			return literal;
		}
|	LOOKAHEAD(logicalAtom())
		literal=logicalAtom() {
		return literal;
	}
	
}
/**
* Read a LogicalAtom from the input stream.
* @return the LogicalAtom read.
*/
Literal logicalAtom() :
{
	Token token;
	Literal logicalAtom;
	Term term;
}
{
	LOOKAHEAD(<LPAREN> <CALL>) 
		term=term() {
			logicalAtom = new Literal("call");
			logicalAtom.add(term);
			return logicalAtom;
		}
|	LOOKAHEAD(<LPAREN> <INVARIANT_CALL>) 
		term=term() {
			logicalAtom = new Literal("call");
			logicalAtom.add(term);
			logicalAtom.setHypothetical(true);
			return logicalAtom;
		}		
|	LOOKAHEAD(<LPAREN> <SYMBOL>)
		<LPAREN> 
			token=<SYMBOL> {
				logicalAtom = new Literal(token.image);
			}
			((term=term()) {
				logicalAtom.add(term);
			})* 
		<RPAREN> {		
			return logicalAtom;
		}
|	LOOKAHEAD(<LPAREN> <INVARIANT_SYMBOL>)
		<LPAREN> 
			token=<INVARIANT_SYMBOL> {
				logicalAtom = new Literal(token.image.substring(1, token.image.length()));
				logicalAtom.setHypothetical(true);
			}
			((term=term()) {
				logicalAtom.add(term);
			})* 
		<RPAREN> {
			return logicalAtom;
		}

}
/**
* Read a Term from the input stream.
* @return the Term read.
*/
Term term():
{
	Token token;
	Term term;
}
{
	LOOKAHEAD(<VARIABLE_SYMBOL>)
		term = variable() {
			return term;
		}
|	LOOKAHEAD(<SYMBOL> | <NUMBER>) 
		term = constant() {
			return term;
		}
|	LOOKAHEAD(<LPAREN> <LIST>)
		term=listTerm() {
			return term;
		}
|	LOOKAHEAD(<LPAREN> <CALL>)
		term=callTerm() {
			return term;
		}
|	LOOKAHEAD(<LPAREN> <INVARIANT_CALL>)
		term=callTerm() {
			((Function)term).setHypothetical(true);
			return term;
		}		
|	LOOKAHEAD(<LPAREN> <SYMBOL>)
		term=function() {
			return term;
		}
|	LOOKAHEAD(<LPAREN> <INVARIANT_SYMBOL>)
		term=function() {
			return term;
		}
}
/**
* Read a Variable from the input stream.
* @return the Variable read.
*/
Variable variable() :
{
	Token token;
}
{
	token=<VARIABLE_SYMBOL> {
		return new Variable(token.image);
	}
}
/**
* Read a Constant from the input stream.
* @return the Constant read.
*/
Constant constant() :
{
	Token token;
}
{
	token=<SYMBOL> {
		return  new Constant(token.image);
	}
|	token=<NUMBER> {
		Constant constant = new Constant(token.image);
		constant.setNumerical(true);
		return constant;
	}	
}		
/**
* Read a list-term from the input stream.
* @return the list-term read.
*/
Term listTerm():
{
	Token token;
	Function function;
	Term term;

}
{
	LOOKAHEAD(3) <LPAREN> <LIST> <RPAREN> {
		return new Constant("nil");
	}
|	<LPAREN> <LIST> term=term() {
		function = new Function(".");
		function.add(term);
		if (term instanceof Variable) function.setGround(false);
	}
	term=list() {
		function.add(term);
		if (term instanceof Variable) {
			function.setGround(false);
		}
		return function;
	}	
}

/**
* Read a list of terms in a list-term from the input stream.
* @return the term representing the list of term read.
*/
Term list():
{
	Token token;
	Term term;
}
{
	<RPAREN> {
		return new Constant("nil");
	}
|	<DOT> term=term() <RPAREN> {
		if (term instanceof Variable
			|| term.getSymbol().equalsIgnoreCase("nil") 
			|| term.getSymbol().equalsIgnoreCase(".")) {
			return term;
		}
		else {
			Function function = new Function(".");
			function.add(term);
			if (term instanceof Variable) {
				function.setGround(false);
			}
			Constant nil = new Constant("nil");
			function.add(nil);
			return function;
		}
	}
| 	term=term() {
		Function function = new Function(".");
		function.add(term);
		if (term instanceof Variable) {
			function.setGround(false);
		}
	}
	term=list() {	
		function.add(term);
		return function;
	}
}
/**
* Read a call-term from the input stream.
* @return the call-term read.
*/
Function callTerm() :
{
	Token token;
	Term term;
	Function function;
}
{
	LOOKAHEAD(<LPAREN> <CALL>) 
	<LPAREN> token=<CALL>
	((token=<SUP> | token=<INF> | token=<SUP_EQUAL> | token=<INF_EQUAL> |
		  token=<MULT> | token=<DIV> | token=<PLUS> | token=<MINUS> | 
		  token=<EQUAL> | token=<NOT_EQUAL> | token=<MIN> | token=<MAX> 
		  | token=<MEMBER> | token=<CAT>) ) {
			function = new Function(token.image);
			function.setEval(true);
		}
		term=term() {
			function.add(term);
			if (term instanceof Variable) {
				function.setGround(false);
			}
		}
		term=term() {
			function.add(term);
			if (term instanceof Variable) {
				function.setGround(false);
			}
		} 
	<RPAREN> {
		return function;
	}
| 	LOOKAHEAD(<LPAREN> <INVARIANT_CALL>) 
	<LPAREN> token=<INVARIANT_CALL>
	((token=<SUP> | token=<INF> | token=<SUP_EQUAL> | token=<INF_EQUAL> |
		  token=<EQUAL> | token=<NOT_EQUAL> | token=<MIN> | token=<MAX> | token=<MEMBER>) ) {
			function = new Function(token.image);
			function.setEval(true);
		}
		term=term() {
			function.add(term);
			if (term instanceof Variable) {
				function.setGround(false);
			}
		}
		term=term() {
			function.add(term);
			if (term instanceof Variable) {
				function.setGround(false);
			}
		} 
	<RPAREN> {
		return function;
	}
}
/**
* Read a Function from the input stream.
* @return the Function read.
*/
Function function():
{
	Token token;
	Term term;
	Function function;
}
{
	LOOKAHEAD(<LPAREN> <SYMBOL>)
		<LPAREN>
			token=<SYMBOL> {
				function = new Function(token.image);
			}
			(term=term() {
				function.add(term);
				if (term instanceof Variable) {
					function.setGround(false);
				}
			})* 
		<RPAREN> {
			return function;
		}
|	LOOKAHEAD(<LPAREN> <INVARIANT_SYMBOL>)
		<LPAREN>
			token=<INVARIANT_SYMBOL> {
				function = new Function(token.image.substring(1, token.image.length()));
				function.setHypothetical(true);
			}
			(term=term() {
				function.add(term);
				if (term instanceof Variable) {
					function.setGround(false);
				}
			})* 
		<RPAREN> {
			return function;
		}
}
